### Low Risk Report for [TraitForge](https://github.com/code-423n4/2024-07-traitforge) by K42

| Issue ID | Description                                                    | Location Link                                                                                                       | Impact | Recommended Mitigation Steps                                   |
| -------- | -------------------------------------------------------------- | ------------------------------------------------------------------------------------------------------------------- | ------ | -------------------------------------------------------------- |
| L-01     | Uncapped `nukeFactor` growth in `NukeFund`                     | [NukeFund.sol](https://github.com/code-423n4/2024-07-traitforge/blob/main/contracts/NukeFund/NukeFund.sol#L136)               | Medium | Insert maximum cap for `nukeFactor`                            |
| L-02     | Integer division in age calculation leads to truncation        | [NukeFund.sol](https://github.com/code-423n4/2024-07-traitforge/blob/main/contracts/NukeFund/NukeFund.sol#L118)               | Low    | Insert fixed-point arithmetic for precise calculations         |
| L-03     | Immediate fund distribution in `DevFund` receive function      | [DevFund.sol](https://github.com/code-423n4/2024-07-traitforge/blob/main/contracts/DevFund/DevFund.sol#L14)                   | Medium | Insert buffer system for undistributed funds                   |
| L-04     | Divergent ETH handling in minting functions                    | [TraitForgeNft.sol](https://github.com/code-423n4/2024-07-traitforge/blob/main/contracts/TraitForgeNft/TraitForgeNft.sol#L181C1-L225C4)     | Low    | Unify ETH handling logic across minting functions              |
| L-05     | Absence of price ceiling in `forgeWithListed`                  | [EntityForging.sol](https://github.com/code-423n4/2024-07-traitforge/blob/main/contracts/EntityForging/EntityForging.sol#L102)      | Low    | Insert maximum price parameter                                 |
| L-06     | Integer multiplication in reward calculation causes truncation | [DevFund.sol](https://github.com/code-423n4/2024-07-traitforge/blob/main/contracts/DevFund/DevFund.sol#L80)                 | Low    | Insert fixed-point arithmetic for reward calculations          |
| L-07     | Risk of reward miscalculation due to integer division in `receive`            | [DevFund.sol]()                       | Low    | Insert in a way more precise calculation method to be sure                   |
| L-08     | Constrained forge potential calculation range                  | [EntityForging.sol](https://github.com/code-423n4/2024-07-traitforge/blob/main/contracts/EntityForging/EntityForging.sol#L86)       | Low    | Expand forge potential calculation range                       |
| L-09     | Non-standard state update sequence in `buyNFT`                 | [EntityTrading.sol](https://github.com/code-423n4/2024-07-traitforge/blob/main/contracts/EntityTrading/EntityTrading.sol#L63)       | Medium | Reorder state updates to adhere to checks-effects-interactions |
| L-10     | Use of block data for entropy generation                       | [EntropyGenerator.sol](https://github.com/code-423n4/2024-07-traitforge/blob/main/contracts/EntropyGenerator/EntropyGenerator.sol#L47) | Medium | Insert external entropy source                                 |

### Low bugs below:

#### L-01: Uncapped `nukeFactor` growth in `NukeFund`

For `NukeFund.sol`, the `calculateNukeFactor` function can allow for unbounded growth:

```solidity
function calculateNukeFactor(uint256 tokenId) public view returns (uint256) {
    uint256 entropy = nftContract.getTokenEntropy(tokenId);
    uint256 adjustedAge = calculateAge(tokenId);
    uint256 initialNukeFactor = entropy / 40;
    uint256 finalNukeFactor = ((adjustedAge * defaultNukeFactorIncrease) / MAX_DENOMINATOR) + initialNukeFactor;
    return finalNukeFactor;
}
```

`finalNukeFactor` will grow indefinitely as `adjustedAge` increases. Unchecked growth like this, opens a path for disproportionately large claims, risking premature fund depletion.

Recommendation:
Insert a maximum cap for `nukeFactor`:

```solidity
import {FixedPointMathLib} from "solmate/src/utils/FixedPointMathLib.sol";

uint256 constant MAX_NUKE_FACTOR = 1e6; // like so

function calculateNukeFactor(uint256 tokenId) public view returns (uint256) {
    // same 
    return FixedPointMathLib.min(finalNukeFactor, MAX_NUKE_FACTOR);
}
```

#### L-02: Integer division in age calculation leads to truncation

For `calculateAge` function in `NukeFund.sol` which uses integer division, in current way, could cause precision loss:

```solidity
function calculateAge(uint256 tokenId) public view returns (uint256) {
    uint256 daysOld = (block.timestamp - nftContract.getTokenCreationTimestamp(tokenId)) / 60 / 60 / 24;
    uint256 perfomanceFactor = nftContract.getTokenEntropy(tokenId) % 10;
    uint256 age = (daysOld * perfomanceFactor * MAX_DENOMINATOR * ageMultiplier) / 365;
    return age;
}
```

Truncation like this may do inaccurate age calculations, particularly for younger tokens, affecting `nukeFactor` calculations and distorting the game economy.

Recommendation:
Insert fixed-point arithmetic for more precise calculations:

```solidity
import {FixedPointMathLib} from "solmate/src/utils/FixedPointMathLib.sol";

function calculateAge(uint256 tokenId) public view returns (uint256) {
    uint256 secondsOld = block.timestamp - nftContract.getTokenCreationTimestamp(tokenId);
    uint256 perfomanceFactor = nftContract.getTokenEntropy(tokenId) % 10;
    uint256 age = FixedPointMathLib.mulDivDown(secondsOld, perfomanceFactor * MAX_DENOMINATOR * ageMultiplier, 365 days);
    return age;
}
```

#### L-03: Immediate fund distribution in `DevFund` receive function

For `DevFund.sol`, the `receive` function distributes funds immediately upon receipt:

```solidity
receive() external payable {
    if (totalDevWeight > 0) {
        uint256 amountPerWeight = msg.value / totalDevWeight;
        uint256 remaining = msg.value - (amountPerWeight * totalDevWeight);
        totalRewardDebt += amountPerWeight;
        if (remaining > 0) {
            (bool success, ) = payable(owner()).call{value: remaining}("");
            require(success, "Failed to send Ether to owner");
        }
    } else {
        (bool success, ) = payable(owner()).call{value: msg.value}("");
        require(success, "Failed to send Ether to owner");
    }
    emit FundReceived(msg.sender, msg.value);
}
```

Current immediate distribution mechanism may lead to uneven fund allocation if developers are added shortly after a distribution event.

Recommendation:
Insert a buffer system for undistributed funds:

```solidity
uint256 public undistributedFunds;

receive() external payable {
    if (totalDevWeight > 0) {
        uint256 amountPerWeight = msg.value / totalDevWeight;
        undistributedFunds += msg.value - (amountPerWeight * totalDevWeight);
        totalRewardDebt += amountPerWeight;
    } else {
        undistributedFunds += msg.value;
    }
    emit FundReceived(msg.sender, msg.value);
}

function distributeBuffer() external onlyOwner {
    require(totalDevWeight > 0, "No developers to distribute to");
    uint256 amountPerWeight = undistributedFunds / totalDevWeight;
    totalRewardDebt += amountPerWeight;
    undistributedFunds = 0;
}
```

#### L-04: Divergent ETH handling in minting functions

For `TraitForgeNft.sol`, `mintToken` and `mintWithBudget` handle excess ETH differently:

```solidity
function mintToken(bytes32[] calldata proof) public payable {
    // mint logic
    uint256 excessPayment = msg.value - mintPrice;
    if (excessPayment > 0) {
        (bool refundSuccess, ) = msg.sender.call{value: excessPayment}("");
        require(refundSuccess, "Refund of excess payment failed.");
    }
}

function mintWithBudget(bytes32[] calldata proof) public payable {
    // handled differently
    if (budgetLeft > 0) {
        (bool refundSuccess, ) = msg.sender.call{value: budgetLeft}("");
        require(refundSuccess, "Refund failed.");
    }
}
```

Above async ETH handling may cause user confusion, when interacting with different minting functions. 

Recommendation:
Unify ETH handling logic across minting functions:

```solidity
function mintToken(bytes32[] calldata proof, bool useEntireBudget) public payable {
    if (useEntireBudget) {
        _mintWithBudget(msg.value);
    } else {
        uint256 mintPrice = calculateMintPrice();
        require(msg.value >= mintPrice, "Insufficient ETH");
        _mintInternal(msg.sender, mintPrice);
        _refundExcess(msg.value - mintPrice);
    }
}

function _refundExcess(uint256 amount) internal {
    if (amount > 0) {
        (bool success, ) = msg.sender.call{value: amount}("");
        require(success, "Refund failed");
    }
}
```

#### L-05: Absence of price ceiling in `forgeWithListed`

For the `forgeWithListed` function in `EntityForging.sol` there is no maximum price check:

```solidity
function forgeWithListed(uint256 forgerTokenId, uint256 mergerTokenId) external payable {
    // same checks here
    uint256 forgingFee = _forgerListingInfo.fee;
    require(msg.value >= forgingFee, "Insufficient fee for forging");
    // more logic
}
```

Absence of using a price ceiling opens a path for users to overpay for forging if the listing price increases between transaction submission, and execution.

Recommendation:
Insert a maximum price parameter:

```solidity
function forgeWithListed(uint256 forgerTokenId, uint256 mergerTokenId, uint256 maxForgingFee) external payable {
    // same checks 
    uint256 forgingFee = _forgerListingInfo.fee;
    require(forgingFee <= maxForgingFee, "Forging fee exceeds maximum");
    require(msg.value >= forgingFee, "Insufficient fee for forging");
    // more logic
}
```

#### L-06: Integer multiplication in reward calculation causes truncation

For `DevFund.sol`, the reward calculation uses integer multiplication, this may enable truncation:

```solidity
uint256 pending = info.pendingRewards + (totalRewardDebt - info.rewardDebt) * info.weight;
```

Truncation in this way, can result in accumulated discrepancies in reward distribution over time, therefore causing unfair distribution of rewards among developers.

Recommendation:
Insert fixed-point arithmetic for more precise calculations:

```solidity
import {FixedPointMathLib} from "solmate/src/utils/FixedPointMathLib.sol";

uint256 pending = info.pendingRewards + FixedPointMathLib.mulWadDown(totalRewardDebt - info.rewardDebt, info.weight);
```

#### L-07: Risk of reward miscalculation due to integer division in `receive`

For `DevFund` contract, the `receive` function uses integer division to calculate the `amountPerWeight`:

```solidity
receive() external payable {
  if (totalDevWeight > 0) {
    uint256 amountPerWeight = msg.value / totalDevWeight;
    uint256 remaining = msg.value - (amountPerWeight * totalDevWeight);
    totalRewardDebt += amountPerWeight;
    if (remaining > 0) {
      (bool success, ) = payable(owner()).call{ value: remaining }('');
      require(success, 'Failed to send Ether to owner');
    }
  } else {
    (bool success, ) = payable(owner()).call{ value: msg.value }('');
    require(success, 'Failed to send Ether to owner');
  }
  emit FundReceived(msg.sender, msg.value);
}
```

The use of integer division in `amountPerWeight = msg.value / totalDevWeight` may open up precision loss, when `msg.value` is not perfectly divisible by `totalDevWeight`.

**Impact:**
The `remaining` amount sent to the owner may be larger than intended, effectively reducing the total rewards distributed to developers.

E.g a scenario where: `totalDevWeight` is `3` and `msg.value` is `10 wei`
The calculation would result in: `amountPerWeight = 10 / 3 = 3` and `remaining = 10 - (3 * 3) = 1`

In this case, `1 wei` is sent to the owner instead of being distributed to developers, and the total reward debt is increased by only `9 wei` instead of `10 wei`.

Recommendation:
Insert in a way more precise calculation method using a larger base unit and fixed-point arithmetic:

```solidity
receive() external payable {
  if (totalDevWeight > 0) {
    uint256 BASE = 1e18;
    uint256 amountPerWeight = (msg.value * BASE) / totalDevWeight;
    totalRewardDebt += amountPerWeight;
    uint256 distributedAmount = (amountPerWeight * totalDevWeight) / BASE;
    uint256 remaining = msg.value - distributedAmount;
    if (remaining > 0) {
      (bool success, ) = payable(owner()).call{ value: remaining }('');
      require(success, 'Failed to send Ether to owner');
    }
  } else {
    (bool success, ) = payable(owner()).call{ value: msg.value }('');
    require(success, 'Failed to send Ether to owner');
  }
  emit FundReceived(msg.sender, msg.value);
}
```

#### L-08: Constrained forge potential calculation range

For `EntityForging.sol`, the forge potential is calculated with a limited range:

```solidity
uint8 forgePotential = uint8((entropy / 10) % 10);
```

Current calculation constrains forge potential to a single digit `0-9`, which may not provide sufficient flexibility for the forging mechanisms, opening up less diverse or less interesting gameplay.

Recommendation:
Expand the range for forge potential:

```solidity
uint16 forgePotential = uint16((entropy % 1000) + 1); // Range: 1-1000
```

#### L-09: Non-standard state update sequence in `buyNFT`

For `buyNFT` function in `EntityTrading.sol` there are updates to state after external calls:

```solidity
function buyNFT(uint256 tokenId) external payable {
    // here
    (bool success, ) = payable(listing.seller).call{value: sellerProceeds}("");
    require(success, "Failed to send to seller");
    nftContract.transferFrom(address(this), msg.sender, tokenId);
    delete listings[listedTokenIds[tokenId]];
}
```

It is good it is protected by `nonReentrant`, but this ordering deviates from the checks-effects-interactions pattern, opening a path for cross-function reentrancy and creative manipulation of the contract state, all it takes it a good black hat.

Recommendation:
To be sure, reorder state updates to adhere to checks-effects-interactions pattern:

```solidity
function buyNFT(uint256 tokenId) external payable {
    // checks here, then 
    delete listings[listedTokenIds[tokenId]];
    (bool success, ) = payable(listing.seller).call{value: sellerProceeds}("");
    require(success, "Failed to send to seller");
    nftContract.transferFrom(address(this), msg.sender, tokenId);
}
```

#### L-10: Use of block data for entropy generation

For the `EntropyGenerator` it uses block data for entropy generation:

```solidity
function writeEntropyBatch1() public {
    // same here
    uint256 pseudoRandomValue = uint256(
        keccak256(abi.encodePacked(block.number, i))
    ) % uint256(10)**78;
    // same here
}
```

Used method now, of entropy generation is vulnerable, to miner manipulation and can be predicted by observers, compromising the fairness and unpredictability of the game mechanics.

Recommendation:
Insert an external entropy source, such as Chainlink VRF:

```solidity
import {VRFConsumerBase} from "@chainlink/contracts/src/v0.8/vrf/VRFConsumerBase.sol";

contract EntropyGenerator is VRFConsumerBase {
    bytes32 internal keyHash;
    uint256 internal fee;
    
    constructor(address _vrfCoordinator, address _link, bytes32 _keyHash, uint256 _fee)
        VRFConsumerBase(_vrfCoordinator, _link)
    {
        keyHash = _keyHash;
        fee = _fee;
    }

    function requestRandomness() internal returns (bytes32 requestId) {
        require(LINK.balanceOf(address(this)) >= fee, "Not enough LINK");
        return requestRandomness(keyHash, fee);
    }

    function fulfillRandomness(bytes32 requestId, uint256 randomness) internal override {
        // Use randomness to generate entropy
    }
}
```

### Some general ones also:

1. **Not enough events used for state changes**: Several functions changing important state variables, do not emit events, this isn't optimal for off-chain tracking of the contract's states.

2. **Risk of DOS in some loop operations**: `writeEntropyBatch1` involves loops that could consume excessive gas when the batch size is large. Use a gas-efficient pattern, or limit the batch size. Next audit, I suggest gas audit. 

3. **Risk of precision loss in division operations**: Multiple calculations involve `/`, in solidity this truncates the result. And opens a path for small inaccuracies that may compound over time, affecting the overall system balance. Use of `/` causes many deeper bugs, so its best to use more optimal patterns.

E.g: 
```solidity
import { SD59x18, sd } from "@prb/math/SD59x18.sol";

// if 'a' and 'b' are SD59x18 numbers
SD59x18 a = sd(1e18); // 1.0 in fixed-point
SD59x18 b = sd(2e18); // 2.0 in fixed-point

// Perform division
SD59x18 result = a.div(b);

// To convert the result back to a uint256, you can use the 'unwrap' function
uint256 unwrappedResult = result.unwrap();
```